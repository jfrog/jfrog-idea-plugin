import java.net.http.HttpClient
import java.net.http.HttpRequest
import java.net.http.HttpResponse
import java.nio.file.Paths

plugins {
    id "org.jetbrains.intellij" version "1.13.0"
    id "java"
    id "maven-publish"
    id "de.undercouch.download" version "5.3.0"
}

group 'com.jfrog.ide'
version currentVersion

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

intellij {
    version = sandboxVersion
    plugins = ['gradle', 'maven', 'Groovy', 'properties', 'java', 'Kotlin', 'org.jetbrains.plugins.go:213.5744.223', 'Pythonid:213.5744.223']
    pluginName = 'JFrog'
    updateSinceUntilBuild = false
}

runPluginVerifier {
    ideVersions = [sandboxVersion]
}

runIde {
    jvmArgs '-Xmx2G'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://releases.jfrog.io/artifactory/oss-releases"
    }
    maven {
        url "https://releases.jfrog.io/artifactory/oss-snapshots"
    }
}

def buildInfoVersion = '2.39.4'
dependencies {
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.14.0'
    implementation group: 'org.jfrog.buildinfo', name: 'build-info-extractor', version: buildInfoVersion
    implementation group: 'org.jfrog.buildinfo', name: 'build-info-client', version: buildInfoVersion
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.14.0'
    implementation group: 'org.jfrog.buildinfo', name: 'build-info-api', version: buildInfoVersion
    implementation group: 'net.lingala.zip4j', name: 'zip4j', version: '2.11.4'
    implementation group: 'com.jfrog.xray.client', name: 'xray-client-java', version: '0.14.0'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
    implementation group: 'org.jfrog.filespecs', name: 'file-specs-java', version: '1.1.2'
    implementation group: 'com.jfrog.ide', name: 'ide-plugins-common', version: '1.13.x-20230223.164532-3'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
    implementation group: 'com.google.guava', name: 'guava', version: '30.1.1-jre'
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: '4.2.0'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.2.0'
    testImplementation 'org.testng:testng:7.7.0'
}

test {
    scanForTestClasses false
    include "**/*Test.class"
    testLogging {
        exceptionFormat "full"
        events "started", "passed", "skipped", "failed", "standardOut", "standardError"
        minGranularity 0
    }
    if (project.hasProperty('excludeTests')) {
        exclude project.property('excludeTests')
    }
}

def webviewFileName = 'jfrog-ide-webview-' + webviewVersion + '.tgz'
def webviewUrl = 'https://releases.jfrog.io/artifactory/ide-webview-npm/jfrog-ide-webview/-/' + webviewFileName
tasks.register('downloadWebview', Download) {
    src webviewUrl
    dest buildDir
    onlyIfModified true
    finalizedBy('getAndUpdateWebviewChecksum')
}

tasks.register('getAndUpdateWebviewChecksum') {
    finalizedBy('verifyWebview')
    if (System.getenv("CI") != null) {
        println 'CI mode is active - Skipping Webview checksum update'
        ext.checksum = webviewChecksum
        return
    }
    ext.checksum = getWebviewChecksumFromServer(webviewUrl)
    updateWebviewChecksumInPropertiesFile(ext.checksum)
}

tasks.register('verifyWebview', Verify) {
    src new File(buildDir, webviewFileName)
    algorithm 'SHA-256'
    checksum getAndUpdateWebviewChecksum.checksum
    finalizedBy('extractWebview')
}

tasks.register('extractWebview', Copy) {
    from tarTree(new File(buildDir, webviewFileName))
    into Paths.get('src', 'main', 'resources', 'jfrog-ide-webview').toFile()
    include '**/build/**/*'
    eachFile {
        path = path.replace('package/build/', '')
    }
}

tasks.withType(JavaCompile).configureEach {
    options.deprecation = true
    options.encoding = "UTF-8"
}

tasks.withType(ProcessResources).configureEach {
    dependsOn('downloadWebview')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact buildPlugin
        }
    }
}

publishPlugin {
    token = System.getenv("JETBRAINS_TOKEN")
}

/**
 * Get Webview checksum from releases.jfrog.io
 * @param webviewUrl - Webview URL
 * @return the sha256 of the webview
 */
static String getWebviewChecksumFromServer(String webviewUrl) {
    def headRequest = HttpRequest.newBuilder(new URL(webviewUrl).toURI()).method("HEAD", HttpRequest.BodyPublishers.noBody()).build()
    def checksumResponse = HttpClient.newHttpClient().send(headRequest, HttpResponse.BodyHandlers.ofString())
    return checksumResponse.headers().firstValue("x-checksum-sha256").get()
}

/**
 * Update the Webview checksum in the gradle.properties file
 * @param checksum - Webview checksum to update
 */
static def updateWebviewChecksumInPropertiesFile(String checksum) {
    def gradleProps = new Properties()
    File gradlePropertiesFile = new File("gradle.properties")
    gradlePropertiesFile.withInputStream { gradleProps.load(it) }
    gradleProps.setProperty("webviewChecksum", checksum)
    gradlePropertiesFile.withWriter('UTF-8') { fileWriter ->
        gradleProps.each { key, value -> fileWriter.writeLine "$key=$value" }
    }
}
